package rules

import com.sample.model.*;


rule "When there is a fire turn on the sprinkler"
when
    Fire($room : room)
    $sprinkler : Sprinkler(room.name == $room.name, on == false )
then
    System.out.println("isi room " + $room.getName());
	System.out.println("rule 1 fired!!!");
    modify( $sprinkler ) { setOn( true ) };
    
    //other way
    //$sprinkler.setOn(true);
    //update($sprinkler);
    
    System.out.println( "Turn on the sprinkler for room " + $room.getName() );
end


rule "When the fire is gone turn off the sprinkler"
when
    $room : Room()
    $sprinkler : Sprinkler( room == $room, on == true )
    not Fire( room == $room )
then
	System.out.println("rule 2 fired!!!");
    modify( $sprinkler ) { setOn( false ) };
    System.out.println( "Turn off the sprinkler for room " + $room.getName() );
end


rule "Raise the alarm when we have one or more fires"
when
    exists Fire()
then
	System.out.println("rule 3 fired!!!");
    insertLogical( new Alarm() );
    System.out.println( "Raise the alarm" );
end


rule "Cancel the alarm when all the fires have gone"
when
    not Fire()
    $alarm : Alarm()
then
	System.out.println("rule 4 fired!!!");
    delete( $alarm );
    System.out.println( "Cancel the alarm" );
end


rule "Status output when things are ok"
when
    not Alarm()
    not Sprinkler( on == true ) 
then
	System.out.println("rule 5 fired!!!");
    System.out.println( "Everything is ok" );
end
